export GPG_TTY=$(tty)
gpg-connect-agent updatestartuptty /bye >/dev/null

setopt extended_glob
setopt histignorealldups
setopt histignorespace

alias ..='cd ..'
alias ...='cd ../..'
alias ....='cd ../../..'
alias e='$VISUAL'
alias h='history'
alias o='xdg-open'

# Use exa instead of ls and tree
alias l='exa --icons --group-directories-first -F'
alias la='exa --icons --group-directories-first -aF'
alias ll='exa --icons --group-directories-first -l'
alias lla='exa --icons --group-directories-first -al'
alias tree='exa --icons -T'

# systemd
alias jc='journalctl -e'
alias sc='systemctl'

# Extract archives
extract() {
	if [ -f "$1" ] ; then
		case "$1" in
			*.tar.bz2)   tar xvjf "$1"    ;;
			*.tar.gz)    tar xvzf "$1"    ;;
			*.tar.xz)    tar xvJf "$1"    ;;
			*.bz2)       bunzip2 "$1"     ;;
			*.rar)       unrar x "$1"     ;;
			*.gz)        gunzip "$1"      ;;
			*.tar)       tar xvf "$1"     ;;
			*.tbz2)      tar xvjf "$1"    ;;
			*.tgz)       tar xvzf "$1"    ;;
			*.zip)       unzip "$1"       ;;
			*.Z)         uncompress "$1"  ;;
			*.7z)        7z x "$1"        ;;
			*.xz)        unxz "$1"        ;;
			*)           echo "\`$1': unrecognized file compression" ;;
		esac
			else
		echo "\`$1' is not a valid file"
	fi
}

# Shell integration for foot terminal
precmd() {
	print -Pn "\e]133;A\e\\" # enable jumping to prompts
}

# Use emacs keybindings
bindkey -e

# Set key bindings
# Create a zkbd compatible hash; to add other keys to this hash, see: man 5 terminfo
typeset -g -A key

key[Home]="${terminfo[khome]}"
key[End]="${terminfo[kend]}"
key[Insert]="${terminfo[kich1]}"
key[Backspace]="${terminfo[kbs]}"
key[Delete]="${terminfo[kdch1]}"
key[Up]="${terminfo[kcuu1]}"
key[Down]="${terminfo[kcud1]}"
key[Left]="${terminfo[kcub1]}"
key[Right]="${terminfo[kcuf1]}"
key[PageUp]="${terminfo[kpp]}"
key[PageDown]="${terminfo[knp]}"
key[Shift-Tab]="${terminfo[kcbt]}"

[[ -n "${key[Home]}"      ]] && bindkey -- "${key[Home]}"       beginning-of-line
[[ -n "${key[End]}"       ]] && bindkey -- "${key[End]}"        end-of-line
[[ -n "${key[Insert]}"    ]] && bindkey -- "${key[Insert]}"     overwrite-mode
[[ -n "${key[Backspace]}" ]] && bindkey -- "${key[Backspace]}"  backward-delete-char
[[ -n "${key[Delete]}"    ]] && bindkey -- "${key[Delete]}"     delete-char
[[ -n "${key[Up]}"        ]] && bindkey -- "${key[Up]}"         up-line-or-history
[[ -n "${key[Down]}"      ]] && bindkey -- "${key[Down]}"       down-line-or-history
[[ -n "${key[Left]}"      ]] && bindkey -- "${key[Left]}"       backward-char
[[ -n "${key[Right]}"     ]] && bindkey -- "${key[Right]}"      forward-char
[[ -n "${key[PageUp]}"    ]] && bindkey -- "${key[PageUp]}"     beginning-of-buffer-or-history
[[ -n "${key[PageDown]}"  ]] && bindkey -- "${key[PageDown]}"   end-of-buffer-or-history
[[ -n "${key[Shift-Tab]}" ]] && bindkey -- "${key[Shift-Tab]}"  reverse-menu-complete

# Finally, make sure the terminal is in application mode, when zle is
# active. Only then the values from $terminfo are valid.
if (( ${+terminfo[smkx]} && ${+terminfo[rmkx]} )); then
	autoload -Uz add-zle-hook-widget
	function zle_application_mode_start { echoti smkx }
	function zle_application_mode_stop { echoti rmkx }
	add-zle-hook-widget -Uz zle-line-init zle_application_mode_start
	add-zle-hook-widget -Uz zle-line-finish zle_application_mode_stop
fi

# Enable completions
autoload -Uz compinit && compinit
zstyle ':completion:*' menu select

# Enable aws completion
autoload -U bashcompinit && bashcompinit
complete -C aws_completer aws

# Enable editor
autoload -Uz edit-command-line
zle -N edit-command-line
bindkey '^X^E' edit-command-line

load() {
	[[ -f "$1" ]] && source "$1"
}

# Enable autosuggestions plugin
load "/usr/share/zsh/plugins/zsh-autosuggestions/zsh-autosuggestions.zsh"

# Enable syntax-highlighting plugin
load "/usr/share/zsh/plugins/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh"

# Enable fuzzy finder
load "/usr/share/fzf/key-bindings.zsh"
load "/usr/share/fzf/completion.zsh"

# Define lfcd command
load "${XDG_CONFIG_HOME}/lf/lfcd.sh"

# Set lf icons
load "${XDG_CONFIG_HOME}/lf/lficons.sh"

# Enable broot
load "${XDG_CONFIG_HOME}/broot/launcher/bash/br"

# Set starship prompt
hash starship 2> /dev/null && eval "$(starship init zsh)"

# Enable zoxide
hash zoxide 2> /dev/null && eval "$(zoxide init zsh)"

unset -f load

# vim: ft=bash
